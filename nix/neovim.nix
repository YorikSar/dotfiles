{
  config,
  pkgs,
  lib,
  ...
}: let
  vim-solarized8 = pkgs.vimUtils.buildVimPluginFrom2Nix {
    pname = "vim-solarized8";
    version = "2023-02-25";
    src = pkgs.fetchFromGitHub {
      owner = "lifepillar";
      repo = "vim-solarized8";
      rev = "bcd4e74e9850fd59ee0294a5c3ae958ed535cc52";
      sha256 = "sha256-Shdyke6V26GOMIVy8FuVTzn3TNMYcvo0razxEIrHEAM=";
    };
  };
in {
  imports = [
    ./languageclient.nix
    ./vim-fugitive.nix
  ];
  programs.neovim = {
    enable = true;
    viAlias = true;
    vimAlias = true;
    extraConfig = builtins.readFile ./extraConfig.vim;
    plugins = with pkgs.vimPlugins; [
      vim-solarized8
      vim-nix
      vim-dispatch
      vim-obsession
      {
        plugin = nvim-surround;
        type = "lua";
        config = ''
          require("nvim-surround").setup({})
        '';
      }
      {
        plugin = nvim-treesitter;
        type = "lua";
        config = ''
          require('nvim-treesitter.configs').setup {
            auto_install = false,
            highlight = {
              enable = true,
              additional_vim_regex_highlighting = false,
            };
          }
        '';
      }
    ];
  };
  programs.git.ignores = [
    "Session.vim" # generated by vim-obsession
  ];
}
